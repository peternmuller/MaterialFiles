/*
 * Copyright (c) 2018 Hai Zhang <dreaming.in.code.zh@gmail.com>
 * All Rights Reserved.
 */

apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-parcelize'

android {
    namespace 'me.zhanghai.android.files'
    buildToolsVersion = '35.0.0'
    compileSdk 35
    ndkVersion '27.1.12297006'
    defaultConfig {
        applicationId 'fr.peternmuller.android.files'
        minSdk 21
        targetSdk 34
        versionCode 39
        versionName '1.7.4'
        resValue 'string', 'app_version', versionName + ' (' + versionCode + ')'
        buildConfigField 'String', 'FILE_PROVIDIER_AUTHORITY', 'APPLICATION_ID + ".file_provider"'
        resValue 'string', 'app_provider_authority', applicationId + '.app_provider'
        resValue 'string', 'file_provider_authority', applicationId + '.file_provider'
        externalNativeBuild {
            cmake {
                arguments '-DANDROID_STL=none'
            }
        }
    }
    buildFeatures {
        aidl true
        buildConfig true
        viewBinding true
    }
    androidResources {
        generateLocaleConfig true
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_21
        targetCompatibility JavaVersion.VERSION_21
    }
    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_21.toString()
    }
    externalNativeBuild {
        cmake {
            path 'CMakeLists.txt'
        }
    }
    lint {
        warning 'InvalidPackage', 'MissingTranslation'
    }
    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    packagingOptions {
        jniLibs {
            useLegacyPackaging true
        }
        resources {
            excludes += [
                    'META-INF/DEPENDENCIES',
                    'org/bouncycastle/pqc/crypto/picnic/*'
            ]
        }
    }
    bundle {
        language {
            enableSplit = false
        }
    }
}

repositories {
    maven {
        url 'https://jitpack.io'
    }
}

dependencies {
    implementation('com.github.zhanghai:dav4jvm:c317607') {
        exclude group: 'org.ogce', module: 'xpp3'
    }
    implementation 'com.github.chrisbanes:PhotoView:2.3.0'
    implementation 'com.github.topjohnwu.libsu:service:5.2.2'
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    def kotlinx_coroutines_version = '1.8.1'
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:$kotlinx_coroutines_version"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:$kotlinx_coroutines_version"
    implementation 'androidx.activity:activity-ktx:1.9.2'
    implementation 'androidx.appcompat:appcompat:1.7.0'
    implementation 'androidx.core:core-ktx:1.13.1'
    implementation 'androidx.drawerlayout:drawerlayout:1.2.0'
    implementation 'androidx.exifinterface:exifinterface:1.3.7'
    implementation 'androidx.fragment:fragment-ktx:1.8.3'
    def androidx_lifecycle_version = '2.8.5'
    implementation "androidx.lifecycle:lifecycle-common-java8:$androidx_lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-livedata-ktx:$androidx_lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-process:$androidx_lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-runtime-ktx:$androidx_lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:$androidx_lifecycle_version"
    implementation 'androidx.preference:preference-ktx:1.2.1'
    implementation 'androidx.recyclerview:recyclerview:1.3.2'
    implementation 'androidx.swiperefreshlayout:swiperefreshlayout:1.1.0'
    implementation 'com.google.android.material:material:1.12.0'

    implementation 'com.caverock:androidsvg-aar:1.4'
    implementation 'com.davemorrissey.labs:subsampling-scale-image-view-androidx:3.10.0'
    implementation 'com.drakeet.drawer:drawer:1.0.3'
    implementation 'com.h6ah4i.android.materialshadowninepatch:materialshadowninepatch:1.0.0'
    implementation 'com.h6ah4i.android.widget.advrecyclerview:advrecyclerview:1.0.0'
    implementation ('com.hierynomus:smbj:0.13.0') {
        exclude group: 'org.bouncycastle', module: 'bcprov-jdk15on'
    }
    implementation ('com.hierynomus:sshj:0.38.0') {
        exclude group: 'org.bouncycastle', module: 'bcprov-jdk15on'
    }
    implementation 'com.jakewharton.threetenabp:threetenabp:1.4.7'
    implementation 'com.leinardi.android:speed-dial:3.3.0'
    implementation ('com.rapid7.client:dcerpc:0.12.1') {
        exclude group: 'com.google.guava', module: 'guava'
        exclude group: 'org.bouncycastle', module: 'bcprov-jdk15on'
    }
    implementation 'com.google.guava:guava:33.3.0-android'
    implementation 'com.google.guava:listenablefuture:9999.0-empty-to-avoid-conflict-with-guava'
    implementation 'com.takisoft.preferencex:preferencex:1.1.0'
    implementation 'commons-net:commons-net:3.11.1'
    implementation 'de.psdev.licensesdialog:licensesdialog:2.2.0'
    implementation 'dev.chrisbanes:insetter-ktx:0.3.1'
    implementation 'dev.rikka.rikkax.preference:simplemenu-preference:1.0.3'
    implementation 'dev.rikka.shizuku:api:13.1.5'
    implementation ('eu.agno3.jcifs:jcifs-ng:2.1.10') {
        exclude group: 'org.bouncycastle', module: 'bcprov-jdk18on'
    }
    implementation 'org.bouncycastle:bcprov-jdk15to18:1.78.1'
    implementation platform('io.coil-kt:coil-bom:2.7.0')
    implementation 'io.coil-kt:coil'
    implementation 'io.coil-kt:coil-gif'
    implementation 'io.coil-kt:coil-svg'
    implementation 'io.coil-kt:coil-video'
    implementation 'me.zhanghai.android.appiconloader:appiconloader:1.5.0'
    implementation 'me.zhanghai.android.fastscroll:library:1.3.0'
    implementation 'me.zhanghai.android.foregroundcompat:library:1.0.2'
    implementation 'me.zhanghai.android.libarchive:library:1.1.0'
    implementation 'me.zhanghai.android.libselinux:library:2.1.0'
    implementation 'me.zhanghai.android.retrofile:library:1.1.1'
    implementation 'me.zhanghai.android.systemuihelper:library:1.0.0'
    implementation 'net.sourceforge.streamsupport:android-retrostreams:1.7.4'
    implementation 'org.apache.ftpserver:ftpserver-core:1.2.0'
    implementation ('org.apache.mina:mina-core') {
        version {
            strictly '2.1.3'
        }
    }
    implementation 'org.slf4j:slf4j-android:1.7.36'
}